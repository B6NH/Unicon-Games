
# Ping

procedure main()

  &window := open("Ping","g","size=640,480","bg=black","fg=white")

  playerHeight := 40
  playerWidth := 10
  ballSide := 10
  height := 480
  width := 640
  maxPaddleY := height - playerHeight

  # Ball position and vector
  x := 340 ; y := 240 ; dx := 1 ; dy := 0

  # Player positions
  p1x := 10 ; p1y := 220
  p2x := 620 ; p2y := 220

  # Player score
  p1score := 0 ; p2score := 0

  # Left Player
  FillRectangle(p1x,p1y,playerWidth,playerHeight)

  # Mid Line
  FillRectangle(318,0,4,height)

  # Right Player
  FillRectangle(p2x,p2y,playerWidth,playerHeight)

  # Ball
  FillRectangle(x,y,ballSide,ballSide)

  repeat {

    # Erase ball
    EraseArea(x,y,ballSide,ballSide)

    # Redraw middle line
    FillRectangle(318,0,4,height)

    # Update ball position
    x +:= dx ; y +:= dy

    # Draw ball at new position
    FillRectangle(x,y,ballSide,ballSide)
    WSync()

    # Bounce left paddle
    # Left ball side and top or bottom side in range
    if p1x <= x <= (p1x + playerWidth) &
       p1y <= (y | y + ballSide) <= (p1y + playerHeight) then {

      dx *:= -1
      dy +:= ((y + 5) - (p1y + 20)) / 15

    }

    # Bounce right paddle
    # Right ball side and top or bottom side in range
    if p2x <= (x + ballSide) <= (p2x + playerWidth) &
       p2y <= (y | y + ballSide) <= (p2y + playerHeight) then {

      dx *:= -1
      dy +:= ((y + 5) - (p2y + 20)) / 15

    }

    # Bounce top wall
    if y <= 0 then
      dy *:= -1

    # Bounce bottom wall
    if (y + ballSide) >= height then
      dy *:= -1

    # Bounce right wall
    if (x + ballSide) > width then {
      dx := dx * -1
      p1score +:= 1
      dy := 0
      GotoXY(10,10)
      writes(&window,p1score)
    }

    # Bounce left wall
    if x <= 0 then {
      dx := dx * -1
      p2score +:= 1
      dy := 0
      GotoXY(600,10)
      writes(&window,p2score)
    }

    delay(5)

    if (*Pending() > 0) then {

      e := Event()

      # Control Player 1

      if e == ("q"|"a") then
        EraseArea(p1x,p1y,10,40)
      if e == "q" then p1y -:= 5
      if e == "a" then p1y +:= 5

      if p1y < 0 then
        p1y := 0
      else if p1y > maxPaddleY then
        p1y := maxPaddleY

      if e == ("q"|"a") then
        FillRectangle(p1x,p1y,10,40)

      # Control Player 2

      if e == ("p"|"l") then
        EraseArea(p2x,p2y,10,40)
      if e == "p" then p2y -:= 5
      if e == "l" then p2y +:= 5

      if p2y < 0 then
        p2y := 0
      else if p2y > maxPaddleY then
        p2y := maxPaddleY

      if e == ("p"|"l") then
        FillRectangle(p2x,p2y,10,40)

    }

  }

end
